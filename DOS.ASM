;	似非DOS

SYSTEM	EQU	00005H
CALSLT	EQU	0001CH
ENASLT	EQU	00024H	
CHGMOD	EQU	0005FH
INITXT	EQU	0006CH
SETTXT	EQU	00078H
FCB1	EQU	0005CH
DTA1	EQU	00080H
FCB11	EQU	0F307H
BDOS	EQU	0F37DH
LINL40	EQU	0F3AEH
BUF	EQU	0F55EH
MEMSIZ	EQU	0F672H
EXPTBL	EQU	0FCC1H
EXTBIOS	EQU	0FFCAH
MAPPER	EQU	00402H

SAVE_SP	EQU	BUF
SAVE_DE	EQU	BUF+2
SAVE_IX	EQU	BUF+4
SAVE_C	EQU	BUF+6
DE_MEM_SIZE	EQU	BUF+7
SAVE_HL	EQU	BUF+8
ALT_MEM	EQU	BUF+10

	ORG	0100H
	CALL	0F36BH		;ページ1をメインRAMに切り替える
	LD	A,0C3H		;JP
	LD	(0),A		;WBOOT
	LD	(SYSTEM),A

	LD	HL,(MEMSIZ)
	LD	L,3
	DEC	H
	LD	(1),HL		;WBOOT
	INC	L
	DEC	H
	DEC	H
	LD	A,H
	LD	SP,HL
	EX	DE,HL
	LD	HL,REAL
	LD	BC,MAIN_END-MAIN
	LDIR
				;リロケータブル
	LD	H,A		;H:+0/A:+0
	LD	L,BDOS0+2
	LD	(HL),A
	INC	A
	INC	A		;A:+2
	LD	L,WBOOT_+2
	LD	(HL),A
	INC	A		;A:+3
	LD	L,EXTSP+1
	LD	(HL),A
	DEC	A		;A:+2
	INC	H
	INC	H		;H:+2
	LD	L,2
	LD	(HL),A
	LD	L,WBOOT+2-BOOT
	LD	(HL),A
	DEC	A		;A:+1
	LD	L,CONST_+2-BOOT
	LD	(HL),A
	LD	L,CONIN+2-BOOT
	LD	(HL),A
	LD	L,CONOUT+2-BOOT
	LD	(HL),A
	LD	L,FCBPAT+2-BOOT
	LD	(HL),A
	LD	L,SHOW_ERROR+2-BOOT
	LD	(HL),A
	DEC	H		;H:+1
	PUSH	HL
	DEC	H		;H:+0
	LD	L,BDOS0
	LD	(SYSTEM+1),HL
	LD	C,19H		;(BDOS)カレント・ドライブ番号の獲得
	CALL	BDOS
	INC	A
	POP	HL		;H:+1
	LD	L,FCBCMD-BOOT
	LD	(HL),A

	LD	A,40
	LD	(LINL40),A
	LD	IY,(EXPTBL)	;メインROMスロット
	LD	IX,INITXT
	CALL	CALSLT
	LD	IY,(EXPTBL)	;メインROMスロット
	LD	IX,SETTXT
	CALL	CALSLT

	LD	DE,TITLE
	LD	C,9
	CALL	BDOS
	LD	C,06FH		;MSX-DOS のバージョン番号の獲得(_DOSVER)
	CALL	BDOS
	LD	A,B
	CP	2
	JR	C,DOS1
	LD	E,'2'
	LD	C,2
	CALL	BDOS
DOS1:
	LD	DE,TITLE2
	LD	C,9
	CALL	BDOS
	LD	BC,9
	LD	HL,AUTOEXEC
	LD	DE,DTA1+1
	LDIR
	LD	HL,BUF
	LD	DE,BUF+1
	LD	(HL),0
	LD	BC,257
	LDIR
	LD	A,9
	LD	HL,(1)
	LD	L,CBOOT & $FF
JP_HL:
	JP	(HL)
TITLE:
	DB	"DOS$"
TITLE2:
	DB	" for MSX v0.04 Gaku$"
AUTOEXEC:
	DB	"AUTOEXEC",0
REAL:
	ORG	4,REAL-0100H
MAIN:
	DW	0
BDOS0:
	JP	BDOS1
BDOS1:
	LD	A,C
	OR	A
WBOOT_:
	JP	Z,WBOOT
BDOS2:
	LD	A,C
	LD	(SAVE_C),A
	CP	00FH		;ファイルオープン
	JR	C,SAVEFCB0
	CP	012H		;ファイル検索 続き
	JR	NZ,NOBD12
	LD	DE,(FCB11)
NOBD12:
	CP	018H		;ログインベクトルの獲得
	JR	C,SAVEFCB36
	CP	021H		;ランダム読み出し
	JR	C,SAVEFCB0
	CP	029H		;ゼロ書き込みを伴うランダム書き込み+1
	JR	C,SAVEFCB36
	CP	043H		;ファイルハンドルのオープン
	JR	Z,SAVE_PATH
	CP	044H		;ファイルハンドルの作成
	JR	Z,SAVE_PATH
	CP	059H		;カレントディレクトリの獲得
	JR	Z,SAVE_PATH
	CP	05BH		;パス名の解析
	JR	Z,SAVE_PATH
	CP	05CH		;ファイル名の解析
	JR	Z,SAVE_PATH
	CP	05EH		;パス文字列全体の獲得
	JR	Z,SAVE_PATH
	CP	066H		;エラーコードの説明
	JR	Z,SAVE_PATH
	JR	SAVEFCB0
SAVEFCB36:
SAVE_PATH:
	LD	A,D
	CP	03FH		;ページ1にかぶる？
	JR	C,SAVEFCB0
	CP	080H
	JR	NC,SAVEFCB0
	LD	A,36
	BIT	6,C		;DOS2のファンクションコール？
	JR	Z,SAVEFCB1
	LD	A,64		;DOS2CALLは64バイト設定する
	JR	SAVEFCB1
SAVEFCB0:
	XOR	A
SAVEFCB1:
	LD	(DE_MEM_SIZE),A
	OR	A
	JR	Z,BDOS3
	PUSH	BC
	PUSH	HL
	LD	(SAVE_DE),DE
	LD	C,A
	LD	B,0
	LD	HL,ALT_MEM
	EX	DE,HL
	PUSH	DE
	LDIR
	XOR	A
	LD	(DE),A
	POP	DE
	POP	HL
	POP	BC
BDOS3:
	XOR	A
	LD	(SAVE_IX+1),A
	LD	A,C
	CP	040H		;最初のエントリ検索
	JR	C,BDOS3IX
	CP	041H+1		;次のエントリ検索
	JR	NC,BDOS3IX
	LD	A,IXH
	CP	03FH		;ページ1にかぶる？
	JR	C,BDOS3IX
	CP	080H		;
	JR	NC,BDOS3IX
	LD	(SAVE_IX),IX
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	POP	HL
	LD	DE,ALT_MEM
	LD	BC,64
	PUSH	DE
	LDIR
	POP	IX
	POP	HL
	POP	DE
	POP	BC
BDOS3IX:
	XOR	A
	LD	(SAVE_HL+1),A
	LD	A,C
	CP	05CH
	JR	NZ,BDOS3HL
	LD	A,H
	CP	03FH		;ページ1にかぶる？
	JR	C,BDOS3HL
	CP	080H		;
	JR	NC,BDOS3HL
	LD	(SAVE_HL),HL
	LD	HL,ALT_MEM
BDOS3HL:
	LD	(SAVE_SP),SP
EXTSP	EQU	$+1
	LD	SP,0
	CALL	BDOS
	LD	SP,(SAVE_SP)

	PUSH	AF

	LD	A,(SAVE_IX+1)		;最初のエントリ検索 + 次のエントリ検索
	OR	A
	JR	Z,BDOS4IX
	LD	IX,(SAVE_IX)
	PUSH	BC
	PUSH	DE
	PUSH	HL
	PUSH	IX
	POP	DE
	LD	HL,ALT_MEM
	LD	BC,64
	LDIR
	POP	HL
	POP	DE
	POP	BC
BDOS4IX:
	LD	A,(DE_MEM_SIZE)
	OR	A
	JR	Z,BDOS4
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	C,A
	LD	B,0
	LD	HL,ALT_MEM
	LD	DE,(SAVE_DE)
	LDIR
	POP	HL
	POP	DE
	POP	BC
BDOS4:
	LD	A,(SAVE_HL+1)
	OR	A
	JR	Z,BDOS5
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	BC,11
	LD	HL,ALT_MEM
	LD	DE,(SAVE_HL)
	LDIR
	POP	HL
	POP	DE
	POP	BC
BDOS5:
	LD	A,(SAVE_C)
	CP	05BH		;パス名の解析
	JR	NZ,BDOS6
	PUSH	BC
	LD	BC,ALT_MEM
	SBC	HL,BC		;CF=0のハズ
	LD	BC,(SAVE_DE)
	ADD	HL,BC
	EX	DE,HL
	LD	BC,ALT_MEM
	AND	A		;念のためにCF=0にする
	SBC	HL,BC
	LD	BC,(SAVE_DE)
	ADD	HL,BC
	EX	DE,HL
	POP	BC
BDOS6:
	POP	AF
	RET

CONST1:
	LD	C,0BH
	JP	SYSTEM
CONIN1:
	LD	C,7
	JP	SYSTEM
CONOUT1:
	LD	E,C
	LD	C,2
	JP	SYSTEM

FCBCMD:
	DB	0,"COMMAND COM",0,0,0,0
ERROR:
	DB	00DH,00AH,"Insert System disk$"
AT:
	DS	0200H-AT
;			下位1バイト0になるように
BOOT:
	JP	WBOOT1
WBOOT:
	JP	WBOOT1
CONST_:			;(BDOS)コンソール直接入力
	JP	CONST1
CONIN:
	JP	CONIN1
CONOUT:
	JP	CONOUT1

WBOOT1:
	XOR	A
CBOOT:
	PUSH	AF
FCBPAT:	LD	HL,FCBCMD
	LD	DE,FCB1
	LD	BC,16
	LDIR

	LD	DE,FCB1
	LD	C,00FH
	CALL	BDOS
	OR	A
	JR	NZ,SHOW_ERROR
	LD	HL,0
	LD	(FCB1+33),HL
	LD	(FCB1+35),HL
	INC	L
	LD	(FCB1+14),HL

	LD	DE,00100H
	LD	C,01AH		;(BDOS)DTAの設定
	CALL	BDOS

	LD	DE,FCB1
	LD	HL,0BF00H
	LD	C,027H		;(BDOS)ランダム・ブロック・リード
	CALL	BDOS
	POP	AF
	LD	(DTA1),A
	LD	A,H
	OR	L

	JP	NZ,0100H
SHOW_ERROR:
	LD	DE,ERROR
	LD	C,9
	CALL	BDOS
	LD	C,8
	CALL	BDOS
	XOR	A
	INC	H
	JR	CBOOT
MAIN_END:
