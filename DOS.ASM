;	似非DOS

SYSTEM	EQU	00005H
CALSLT	EQU	0001CH
ENASLT	EQU	00024H	
CHGMOD	EQU	0005FH
INITXT	EQU	0006CH
SETTXT	EQU	00078H
FCB1	EQU	0005CH
DTA1	EQU	00080H
FCB11	EQU	0F307H
BDOS	EQU	0F37DH
LINL40	EQU	0F3AEH
BUF	EQU	0F55EH
MEMSIZ	EQU	0F672H
EXPTBL	EQU	0FCC1H
EXTBIOS	EQU	0FFCAH
MAPPER	EQU	00402H

	ORG	0100H
	CALL	0F36BH		;ページ1をメインRAMに切り替える
	LD	A,0C3H		;JP
	LD	(0),A
	LD	(SYSTEM),A

	LD	DE,(MEMSIZ)
	LD	E,3
	DEC	D
	LD	(1),DE
	DEC	D
	LD	A,D
	LD	(SPPAT+2),A
SPPAT:	LD	SP,4
	LD	A,D
	LD	E,4
	LD	HL,REAL
	LD	BC,MAIN_END-MAIN
	LDIR
				;リロケータブル
	LD	H,A
	LD	L,BDOS0+2
	LD	(HL),A
	LD	L,SAVEFCB1+2
	LD	(HL),A
	LD	L,ALTFCB0+2
	LD	(HL),A
	LD	L,ALTFCB1+2
	LD	(HL),A
	LD	L,_SAVE_DE+3
	LD	(HL),A
	LD	L,SAVE_SP+3
	LD	(HL),A
	INC	H
	LD	L,2
	LD	(HL),A
	LD	L,WBOOT+2-BOOT
	LD	(HL),A
	LD	L,CONST_+2-BOOT
	LD	(HL),A
	LD	L,CONIN+2-BOOT
	LD	(HL),A
	LD	L,CONOUT+2-BOOT
	LD	(HL),A
	DEC	H
	INC	A
	LD	L,FCBPAT+2-BOOT
	LD	(HL),A
	LD	L,SHOW_ERROR+2
	LD	(HL),A
	INC	A
	LD	L,EXTSP+1
	LD	(HL),A
	SUB	2
	LD	L,BDOS0
	LD	(SYSTEM+1),HL
	PUSH	HL
	LD	C,19H		;(BDOS)カレント・ドライブ番号の獲得
	CALL	BDOS
	INC	A
	POP	HL
	INC	H
	LD	L,FCBCMD-BOOT
	LD	(HL),A

	LD	A,40
	LD	(LINL40),A
	LD	IY,(EXPTBL)	;メインROMスロット
	LD	IX,INITXT
	CALL	CALSLT
	LD	IY,(EXPTBL)	;メインROMスロット
	LD	IX,SETTXT
	CALL	CALSLT

	LD	DE,TITLE
	LD	C,9
	CALL	BDOS
	LD	BC,9
	LD	HL,AUTOEXEC
	LD	DE,00081H
	LDIR
	LD	HL,BUF
	LD	DE,BUF+1
	LD	(HL),0
	LD	BC,257
	LDIR
	LD	A,9
	LD	HL,(SYSTEM+1)
	LD	L,CBOOT & $FF
JP_HL:
	JP	(HL)
TITLE:
	DB	"DOS for MSX v0.02 Gaku$"
AUTOEXEC:
	DB	"AUTOEXEC",0
REAL:
	ORG	4,REAL-0100H
MAIN:
	DW	0
BDOS0:
	JP	BDOS1
BDOS1:
	LD	A,C
	OR	A
	JR	NZ,BDOS2
WBOOT1:
	XOR	A
CBOOT:
	PUSH	AF
FCBPAT:	LD	HL,FCBCMD
	LD	DE,FCB1
	LD	BC,16
	LDIR

	LD	DE,FCB1
	LD	C,00FH
	CALL	BDOS
	OR	A
	JR	NZ,SHOW_ERROR
	LD	HL,0
	LD	(FCB1+33),HL
	LD	(FCB1+35),HL
	INC	L
	LD	(FCB1+14),HL

	LD	DE,00100H
	LD	C,01AH		;(BDOS)DTAの設定
	CALL	BDOS

	LD	DE,FCB1
	LD	HL,0BF00H
	LD	C,027H		;(BDOS)ランダム・ブロック・リード
	CALL	BDOS
	POP	AF
	LD	(DTA1),A
	LD	A,H
	OR	L

	JP	NZ,0100H
SHOW_ERROR:
	LD	DE,ERROR
	LD	C,9
	CALL	BDOS
	LD	C,8
	CALL	BDOS
	XOR	A
	JR	CBOOT
BDOS2:
	LD	A,C
	CP	00FH
	JR	C,SAVEFCB0
	CP	012H
	JR	NZ,NOBD12
	LD	DE,(FCB11)
NOBD12:
	CP	018H
	JR	C,SAVEFCB36
	CP	021H
	JR	C,SAVEFCB0
	CP	029H
	JR	C,SAVEFCB36
	JR	SAVEFCB0
SAVEFCB36:
	LD	A,D
	CP	03FH
	JR	C,SAVEFCB0
	CP	080H
	JR	NC,SAVEFCB0
	LD	A,36
	JR	SAVEFCB1
SAVEFCB0:
	XOR	A
SAVEFCB1:
	LD	(SAVEFCB),A
	OR	A
	JR	Z,BDOS3
	PUSH	BC
	PUSH	HL
_SAVE_DE:
	LD	(SAVE_DE),DE
	LD	C,A
	LD	B,0
ALTFCB0:
	LD	HL,ALTFCB
	EX	DE,HL
	PUSH	DE
	LDIR
	POP	DE
	POP	HL
	POP	BC
BDOS3:
SAVE_SP:
	LD	(BDSP),SP
EXTSP	EQU	$+1
	LD	SP,0
	CALL	BDOS
BDSP	EQU	$+1
	LD	SP,0

	PUSH	AF
SAVEFCB	EQU	$+1
	LD	A,0
	OR	A
	JR	Z,BDOS4
	PUSH	BC
	PUSH	DE
	PUSH	HL
	LD	C,A
	LD	B,0
ALTFCB1:
	LD	HL,ALTFCB
SAVE_DE	EQU	$+1
	LD	DE,0
	LDIR
	POP	HL
	POP	DE
	POP	BC
BDOS4:
	POP	AF
	RET

CONST1:
	LD	C,0BH
	JP	SYSTEM
CONIN1:
	LD	C,7
	JP	SYSTEM
CONOUT1:
	LD	E,C
	LD	C,2
	JP	SYSTEM

ALTFCB:
	DS	37
	DS	5
;			下位1バイト0になるように
BOOT:
	JP	WBOOT1
WBOOT:
	JP	WBOOT1
CONST_:			;(BDOS)コンソール直接入力
	JP	CONST1
CONIN:
	JP	CONIN1
CONOUT:
	JP	CONOUT1
FCBCMD:
	DB	0,"COMMAND COM",0,0,0,0
ERROR:
	DB	00DH,00AH,"Ins DOS$"
MAIN_END:
